// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    // Android Gradle Plugin 8.5.1+ required for 16 KB page size support (Android 15+)
    // This ensures native libraries are properly aligned for 16 KB devices
    classpath('com.android.tools.build:gradle:8.5.1')
    classpath('com.facebook.react:react-native-gradle-plugin')
    classpath('org.jetbrains.kotlin:kotlin-gradle-plugin')
    classpath('com.google.gms:google-services:4.4.2')
  }
}

def reactNativeAndroidDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('react-native/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android"
)

allprojects {
  repositories {
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url(reactNativeAndroidDir)
    }

    google()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
  }
}

subprojects { subproject ->
  if (subproject.name == "react-native-freshchat-sdk") {
    subproject.afterEvaluate {
      dependencies {
        implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0"
      }
    }
  }
}

apply plugin: "expo-root-project"
apply plugin: "com.facebook.react.rootproject"
